WHITESPACE = _{ " " | "\n" | "\t" }
COMMENT = _{ 
    "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) | 
    "/*" ~ (!"*/" ~ ANY)* ~ "*/" 
}
semi = { ";" }

program = { SOI ~ declaration* ~ EOI }

// Declarations
declaration = {
    class_decl | fun_decl | statement
}

class_decl = {
    "class" ~ ident ~ ("<" ~ ident)? ~ "{" ~ function* ~ "}"
}

fun_decl = {
    "fun" ~ function
}

var_decl = {
    "var" ~ ident ~ ("=" ~ expr)? ~ ";"
}

// Statements
statement = {
    var_decl |
    print_stmt |
    expr_stmt |
    for_stmt |
    if_stmt |
    return_stmt |
    while_stmt |
    block
}

expr_stmt = { expr ~ ";" }
for_stmt = { 
    "for" ~ "(" ~ (var_decl | expr ~ semi | semi )
    ~ expr? ~ semi
    ~ expr? ~ ")"
    ~ block
}
while_stmt = {
    "while" ~ expr ~ block
}
if_stmt = {
    "if" ~ expr ~ block ~ ("else" ~ block)? 
}
print_stmt = {
    "print" ~ expr ~ ";"
}
return_stmt = {
    "return" ~ expr? ~ ";"
}
block = { "{" ~ declaration* ~ "}" }

// Operators:
op_unary_minus =  { "-" }
op_unary_not   =  { "!" }
op_unary       = _{
    op_unary_minus |
    op_unary_not
}

op_plus          =  { "+" }
op_minus         =  { "-" }
op_times         =  { "*" }
op_divide        =  { "/" }
op_and           =  { "and" }
op_or            =  { "or" }
op_greater       =  { ">" }
op_greater_equal =  { ">=" }
op_lower         =  { "<" }
op_lower_equal   =  { "<=" }
op_equal         =  { "==" }
op_not_equal         =  { "!=" }
op_assign = { "=" }
op_infix         = _{
    op_plus |
    op_minus |
    op_times |
    op_divide |
    op_and |
    op_or |
    op_greater_equal |
    op_greater |
    op_lower_equal |
    op_lower |
    op_equal |
    op_assign |
    op_dot
}

paren_open  = _{ "(" }
paren_close = _{ ")" }

// value = {
//     float |
//     int |
//     string |
//     ident |
//     paren_open ~ expr ~ paren_close
// }

op_dot   =  { "." }
comma =  _{ "," }
args  = _{ expr ~ (comma ~ expr)* }
call  =  { ident ~ paren_open ~ args? ~ paren_close }

value = {
    rtrue |
    rfalse |
    float | // float comes before int since they overlap
    int |
    string |
    ident |
    paren_open ~ expr ~ paren_close |
    call
}

term = { op_unary* ~ (call | value) }
// path = { ident ~ (op_dot ~ ident)* }

// assignment = {  ~ "=" ~ expr }

expr = { term ~ (op_infix ~ term)* }

// Utility
function = { ident ~ "(" ~ parameters? ~ ")" ~ block }
parameters = { ident ~ ("," ~ ident )* }

// Terminals 
rtrue = @{ "true" }
rfalse = @{ "false" }
digit = _{ '0'..'9' }
int   = @{ digit ~ (digit | "_")* }
float = @{ int ~ "." ~ int? }
quote = _{ "\"" }
string = @{ quote ~ (!"\"" ~ ANY)* ~ quote }
ident = @{ ALPHA ~ (ALPHA | DIGIT)* }
DIGIT = _{ ASCII_DIGIT }
ALPHA = _{ 'a'..'z' | 'A'..'Z' | "_" }
